/*
 * il_parser.y - parse Intermediate Language
 *
 *   Copyright (c) 2008, Ueda Laboratory LMNtal Group <lmntal@ueda.info.waseda.ac.jp>
 *   All rights reserved.
 *
 *   Redistribution and use in source and binary forms, with or without
 *   modification, are permitted provided that the following conditions are
 *   met:
 *
 *    1. Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *
 *    2. Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in
 *       the documentation and/or other materials provided with the
 *       distribution.
 *
 *    3. Neither the name of the Ueda Laboratory LMNtal Group nor the
 *       names of its contributors may be used to endorse or promote
 *       products derived from this software without specific prior
 *       written permission.
 *
 *   THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 *   "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 *   LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 *   A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 *   OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 *   SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 *   LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 *   DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 *   THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 *   (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 *   OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 *
 * $Id: il_parser.y,v 1.9 2008/09/29 05:23:40 taisuke Exp $
 */

%{
#include <math.h>
#include <stdio.h>
#include "syntax.hpp"
#include "element/element.h"
#include "il_lexer.hpp"
%}

%pure-parser
%locations
%parse-param {il::lexer* scanner}
%parse-param {ILRef* ret_il}
%parse-param {RuleRef* ret_rule}
/* ローカルのルールセットIDからグローバルなIDへのテーブル */
%lex-param {il::lexer* scanner}

%union {              /* define stack type */
  ArgList args;
  FunctorRef functor;
  ILRef il;
  InstBlockRef inst_block;
  InstList instructions;
  InstrArgRef arg;
  InstructionRef instruction;
  RuleList rules;
  RuleRef rule;
  RuleSetRef ruleset;
  VarList inst_vars;
  double _float;
  int _int;
  lmn_interned_str id;
  lmn_interned_str str;
  std::vector<lmn_interned_str> *inline_list;
  std::vector<std::shared_ptr<RuleSet>> *rulesets;
  std::vector<std::unique_ptr<Module>> *module_list;
}

%token <_int> INT
%token <_float> FLOAT
%token <id> ID
%token <_int> INST_NAME
%token <_int> LABEL
%token <str> DQUOTED_STRING
%token <str> SQUOTED_STRING
%token <_int> RULESET_ID
%token COMMA PERIOD COLON UNDERBAR LBRACE RBRACE LBRACKET RBRACKET
%token INSIDE_PROXY OUTSIDE_PROXY
%token KW_COMPILED_RULESET KW_COMPILED_SYSTEM_RULESET KW_COMPILED_UNIQ_RULE KW_COMPILED_RULE KW_ATOMMATCH KW_MEMMATCH
%token KW_GUARD KW_BODY KW_INLINE KW_MODULE

%token <_int> INST_TK_LOOP
%token <_int> INST_TK_RUN
%token <_int> INST_TK_NOT
%token <_int> INST_TK_GROUP
%token <_int> INST_TK_BRANCH

%token _EOF 0

%start start
%type <il> world
%type <module_list> module_def;
%type <module_list> modules;
%type <rulesets> ruleSetList
%type <ruleset> ruleSet
%type <rule> rule
%type <inst_block> atommatch
%type <inst_block> memmatch
%type <inst_block> guard
%type <inst_block> body
%type <instruction> instruction
%type <instructions> instructions
%type <rules> ruleList
%type <_int> label
%type <arg> arg
%type <functor> functor
%type <inst_vars> var_list;
%type <inst_vars> var_list_;
%type <arg> var_list_arg;
%type <args> args
%type <args> args_
%type <args> inst_list_arg
%type <inline_list> inlines;

%{
#include "il_lexer.hpp"
int illex(YYSTYPE *yylval, YYLTYPE *yyloc, il::lexer *lexer);
void ilerror (YYLTYPE*, il::lexer*, ILRef *, RuleRef *, const char *);
%}

%% /* Grammar rules and actions follow.  */

start:
  world _EOF { *ret_il = $1; }   /* 中間言語全体のパース */
| rule { *ret_rule = $1; }  /* ルールのみの入力をパース */
;

world:
  ruleSetList { $$ = new IL($1); }
| ruleSetList module_def { $$ = new IL($1, $2, new std::vector<lmn_interned_str>); }
| ruleSetList module_def KW_INLINE inlines { $$ = new IL($1, $2, $4); }
| ruleSetList KW_INLINE inlines { $$ = new IL($1, new std::vector<std::unique_ptr<Module>>, $3); }
;

module_def:
KW_MODULE modules { $$ = $2; }
;

modules:
 /*empty*/ { $$ = new std::vector<std::unique_ptr<Module>>; }
| modules SQUOTED_STRING LBRACE  RBRACE { $$ = $1; } /* この場合もある */
| modules SQUOTED_STRING LBRACE RULESET_ID RBRACE {
  $1->push_back(std::unique_ptr<Module>(new Module($2, $4)));
  $$ = $1;
}
;

inlines:
  /*empty*/ { $$ = new std::vector<lmn_interned_str>; }
| inlines DQUOTED_STRING { $1->push_back($2); $$ = $1; }
;

ruleSetList:
ruleSet { $$ = new std::vector<std::shared_ptr<RuleSet>>; $$->push_back(std::shared_ptr<RuleSet>($1)); }
| ruleSetList ruleSet { $1->push_back(std::shared_ptr<RuleSet>($2)); $$ = $1; }
;

ruleSet:
  KW_COMPILED_SYSTEM_RULESET RULESET_ID ruleList { $$ = new RuleSet($2, $3, TRUE); }
| KW_COMPILED_RULESET RULESET_ID ruleList { $$ = new RuleSet($2, $3, FALSE); }
;

ruleList:
  rule { $$ = new __RuleList; $$->push_back($1); }
| ruleList rule { $1->push_back($2); $$ = $1; }
;

rule:
  KW_COMPILED_UNIQ_RULE atommatch memmatch guard body {
    $$ = new Rule(TRUE, $2, $3, $4, $5);
  }
| KW_COMPILED_RULE atommatch memmatch guard body {
    $$ = new Rule(FALSE, $2, $3, $4, $5);
  }
;

atommatch:
KW_ATOMMATCH COLON instructions { $$ = new InstBlock($3); }
;

memmatch:
  KW_MEMMATCH COLON instructions { $$ = new InstBlock($3); }
;

guard:
/* empty */ { $$ = new InstBlock(new __InstList); }
|  KW_GUARD COLON label COLON instructions { $$ = new InstBlock($3, $5); }
;

body:
/* empty */ { $$ = new InstBlock(new __InstList); }
|  KW_BODY COLON label COLON instructions { $$ = new InstBlock($3, $5); }
;

label:
  LABEL { $$ = $1; }
;

instructions:
  /* empty */ { $$ = new __InstList; }
| instructions instruction { $1->push_back($2); $$ = $1; }
;

 /* COMMITを特別にinst_makeで処理しているので注意 */
instruction:
 INST_NAME LBRACKET args RBRACKET {
  $$ = new Instruction((LmnInstruction)$1, $3);
}
  /* 変数のリストと命令のリストがreduce/reduce コンフリクトを起こすので別に扱う */
|  INST_TK_LOOP LBRACKET inst_list_arg RBRACKET { $$ = new Instruction((LmnInstruction)$1, $3); }
|  INST_TK_RUN LBRACKET inst_list_arg RBRACKET { $$ = new Instruction((LmnInstruction)$1, $3); }
|  INST_TK_NOT LBRACKET inst_list_arg RBRACKET { $$ = new Instruction((LmnInstruction)$1, $3); }
|  INST_TK_GROUP LBRACKET inst_list_arg RBRACKET { $$ = new Instruction((LmnInstruction)$1, $3); }
|  INST_TK_BRANCH LBRACKET inst_list_arg RBRACKET { $$ = new Instruction((LmnInstruction)$1, $3); }
;

arg:
  INT { $$ = InstrArg::create<InstrVar>($1); }
| functor { $$ = InstrArg::create<ArgFunctor>($1); }
| RULESET_ID { $$ = InstrArg::create<ArgRuleset>($1); }
| DQUOTED_STRING { $$ = InstrArg::create<String>($1); }
| label { $$ = InstrArg::create<Label>($1); }
| var_list_arg { $$ = $1; }
;

args:
/*empty*/ { $$ = new __ArgList; }
// | args_ arg { $1->push_back($2); $$ = $$ = $1; }
| args_ arg { $1->push_back($2); $$ = $1; }
;

args_:
/*empty*/ { $$ = new __ArgList; }
| args_ arg COMMA { $1->push_back($2) ; $$ = $1; }
;

var_list_arg:
LBRACKET var_list RBRACKET { $$ = InstrArg::create<InstrVarList>($2); }
;

var_list:
/*empty*/  { $$ = new __VarList; }
| var_list_ INT { $1->push_back(InstrArg::create<InstrVar>($2)); $$ = $1; }
| var_list_ functor { $1->push_back(InstrArg::create<ArgFunctor>($2)); $$ = $1; }
;

var_list_:
/*empty*/ { $$ = new __VarList; }
| var_list_ INT COMMA { $1->push_back(InstrArg::create<InstrVar>($2)); $$ = $1; }
| var_list_ functor COMMA { $1->push_back(InstrArg::create<ArgFunctor>($2)); $$ = $1; }
;

inst_list_arg:
LBRACKET instructions RBRACKET {
  ArgList l = new __ArgList;
  l->push_back(InstrArg::create<InstrList>($2));
  $$ = l;
}
;

functor:
  INSIDE_PROXY { $$ = new Functor(STX_IN_PROXY); }
| OUTSIDE_PROXY { $$ = new Functor(STX_OUT_PROXY); }
| SQUOTED_STRING UNDERBAR INT {$$ = new Functor($1, $3); }
| DQUOTED_STRING UNDERBAR INT {$$ = new Functor($1); }
| SQUOTED_STRING PERIOD SQUOTED_STRING UNDERBAR INT {
    $$ = new Functor($1, $3, $5);
  }
| INT UNDERBAR INT {
    $$ = new Functor((long)$1);
  }
| FLOAT UNDERBAR INT {
    $$ = new Functor($1);
  }
;

%%


/* Called by yyparse on error.  */
void ilerror (YYLTYPE *loc, il::lexer* scanner, ILRef *il, RuleRef *rule, const char *s)
{
  fprintf (stderr, "il parser: error %s line: %d\n", s, scanner->lineno());
  exit(1);
}
